
buildscript {
    ext.kotlin_version = '1.0.3'
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M2'
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    jcenter()
}

configurations {
    javascript
}

dependencies {
    compile project(':resources_en')
    compile group: 'org.json', name: 'json', version: '20160810'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.8.2'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    javascript project(':server')

    testCompile "com.winterbe:expekt:0.5.0"
    testCompile 'org.jetbrains.spek:spek-api:1.0.89'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.0.89'
}

intellij {
    version 'IU-2016.1'
    type 'IU'
    updateSinceUntilBuild false
    plugins = ['JavaScriptLanguage']
}

junitPlatform {
    engines {
        include 'spek'
    }
}

task copyJs(type: Copy, dependsOn: configurations.javascript) {
    from zipTree(configurations.javascript.singleFile)
    into { "$prepareSandbox.destinationDir/$project.name/" }
}

afterEvaluate {
    prepareSandbox.dependsOn copyJs
    build.dependsOn buildPlugin
}
